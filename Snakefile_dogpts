from itertools import chain, combinations
from os.path import join
import glob
import re

RAWDATA_DIR = '/vol_b/rawdata/dog_pts'

SAMPLE_NAMES = ['Canine_NK_Day_0_HVE5_2_15_19', 'Canine_NK_Day_0_IRS4_12_14_18', 'Canine_NK_Day_0_KPC6_11_27_18', 'Canine_NK_Day_0_VBE7_12_14_18', 'Canine_NK_Day_14_Co_Culture_EKA5_12_11_18', 'Canine_NK_Day_14_Co_Culture_IRS4_12_28_18', 'Canine_NK_Day_14_Co_Culture_KPC6_12_11_18', 'Canine_NK_Day_14_Co_Culture_VBE7_12_28_18', 'Canine_NK_Day_3_IL_15_HVE5_2_18_19', 'Canine_NK_Day_3_IL_15_IRS4_12_17_18', 'Canine_NK_Day_3_IL_15_KPC6_2_18_19', 'Canine_NK_Day_3_IL_15_VBE7_2_18_19', 'Dazey_041619_bright', 'Dazey_041619_dim', 'Dazey_042615_bright', 'Dazey_042615_dim', 'Dazey_050319_bright', 'Dazey_050319_dim', 'Dazey_051719_bright', 'Dazey_051719_dim', 'Emma_01082019_bright', 'Emma_01082019_dim', 'Emma_12022018_dim', 'Emma_12062018_bright', 'Emma_12172018_bright', 'Emma_12172018_dim', 'Emma_12212018_bright', 'Emma_12212018_dim', 'Iggy01032019_dim', 'Iggy_01032019_bright', 'Iggy_11292018_bright', 'Iggy_11292018_dim', 'Iggy_12112018_bright', 'Iggy_12112018_dim', 'Iggy_12192018_bright', 'Iggy_12192018_dim', 'Morgan_112118_dim', 'Morgan_120518_bright', 'Morgan_120518_dim', 'Morgan_121118_bright', 'Morgan_121118_dim', 'Morgan_122818_bright', 'Morgan_122818_dim', 'Nick_062419_bright', 'Nick_062419_dim', 'Nick_070119_bright', 'Nick_070119_dim', 'Nick_070819_bright', 'Nick_070819_dim', 'Santino_032519_bright', 'Santino_032519_dim', 'Santino_040119_bright', 'Santino_040119_dim', 'Santino_040819_bright', 'Santino_040819_dim', 'Santino_042619_bright', 'Santino_042619_dim', 'Sessy_071619_bright', 'Sessy_071619_dim', 'Sessy_072319_bright', 'Sessy_072319_dim', 'Sessy_073019_bright', 'Sessy_073019_dim']


rule all:
    input: 
        expand('outputs/quant_cf_nonribo/{sample}_quant/quant.sf', sample = SAMPLE_NAMES),
		expand('outputs/ribo/{sample}-nonribo.qc.fq.gz', sample = SAMPLE_NAMES)

rule cat_fastq:
    output: 'inputs/cat/{sample}.fq.gz'
    params: indir = RAWDATA_DIR
    shell:'''
    cat {params.indir}/{wildcards.sample}_S*_L00*_R1_001.fastq.gz > {output} 
    '''

rule first_12_qc:
    output: 'outputs/quality/{sample}.trim.fq.gz'
    input: 'inputs/cat/{sample}.fq.gz'
    conda: 'env.yml'
    shell:''' 
    trimmomatic SE {input} {output} LEADING:12
    '''
    
rule bbduk_qc:
    output: 'outputs/quality/{sample}.trimbb.fq.gz'
    input:
        fq = 'outputs/quality/{sample}.trim.fq.gz',
        polyA = 'inputs/polyA.fa.gz',
        truseqr = 'inputs/truseq_rna.fa.gz',
        truseq = 'inputs/truseq.fa.gz'
    conda: 'env.yml'
    shell:'''
    bbduk.sh in={input.fq} out={output} ref={input.polyA},{input.truseq},{input.truseqr} k=13 ktrim=r useshortkmers=t mink=5 qtrim=r trimq=10 minlength=20
    '''

rule bbduk_find_ribo:
    output:
        ribo='outputs/ribo/{sample}-ribo.qc.fq.gz',
        nonribo='outputs/ribo/{sample}-nonribo.qc.fq.gz'
    input: 
        reads='outputs/quality/{sample}.trimbb.fq.gz',
        ribo='inputs/humanribo.fas'
    conda: 'env.yml'
    shell:'''
    bbduk.sh -Xmx4g in={input.reads} outm={output.ribo} outu={output.nonribo} k=31 ref={input.ribo}
    '''

rule download_cf_transcriptome:
    output: 'inputs/transcriptomes/GCF_000002285.3_CanFam3.1_rna.fna.gz'
    shell:'''
    curl -L ftp://ftp.ncbi.nlm.nih.gov/genomes/all/GCF/000/002/285/GCF_000002285.3_CanFam3.1/GCF_000002285.3_CanFam3.1_rna.fna.gz -o {output}
    '''

rule index_cf:
    output: directory('inputs/transcriptomes/cf_index')
    input: 'inputs/transcriptomes/GCF_000002285.3_CanFam3.1_rna.fna.gz'
    conda: 'env.yml'
    shell:'''
    salmon index -t {input} -i {output}
    '''

rule salmon_cf:
    output: 'outputs/quant_cf_nonribo/{sample}_quant/quant.sf',
    input:
        fq = 'outputs/ribo/{sample}-nonribo.qc.fq.gz',
        indx = 'inputs/transcriptomes/cf_index'
    conda: 'env.yml'
    params: outdir = "outputs/quant_cf_nonribo/{sample}_quant/"
    shell:'''
    salmon quant -i {input.indx} -l A -r {input.fq} --writeUnmappedNames -o {params.outdir}
    '''